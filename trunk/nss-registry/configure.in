#Svn stuff
# $Id$
# $LastChangedBy$

#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT([libnss_registry], [0.4], [rayman@skumler.net])
AC_CONFIG_SRCDIR([src/lib.c])
AM_CONFIG_HEADER([src/config.h])

AC_PREFIX_DEFAULT(/usr)
AM_INIT_AUTOMAKE([nss-registry],[0.4])

AC_CANONICAL_HOST
slibdir=NONE
case "$host" in
*linux* | *Linux*)
  # The Linux filesystem standard prescribed where to place "essential"
  # files.  I.e., when the installation prefix is "/usr" we have to place
  # shared library objects on the root partition in /lib.
  if test "$prefix" = "/usr" -o "$prefix" = "NONE"; then
    # 64bit libraries on sparc go to /lib64 and not /lib
    if test "$host_cpu" = "sparc64"; then
      slibdir="/lib64"
      if test "$libdir" = '${exec_prefix}/lib'; then
        libdir='${exec_prefix}/lib64';
      fi
    else
      slibdir="/lib"
    fi
  fi
  ;;
esac
if test "$slibdir" = "NONE"; then
  slibdir="/lib"
fi

# Checks for programs.
CFLAGS="${CFLAGS}"
AC_PROG_CC
AC_PROG_INSTALL
AM_DISABLE_STATIC
AM_PROG_LIBTOOL

# Checks for libraries.
AC_CHECK_LIB(registry,registryOpen,,AC_MSG_ERROR(No registry support found))

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h string.h syslog.h unistd.h])
AC_CHECK_HEADERS([nsswitch.h shadow.h pwd.h])

AC_CHECK_HEADER(registry.h,,AC_MSG_ERROR(Registry headers not available))

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_TYPE_UID_T

# Checks for library functions.
AC_FUNC_MALLOC
AC_CHECK_FUNCS([strdup strtol])

#debug disabled by default
AC_ARG_ENABLE(debug,
  AC_HELP_STRING([--enable-debug],[Enable debug support]),
  [case "${enable_debug}" in
    yes) use_debug=1 
	 debug_parms="-g3 -ggdb -DDEBUG" ;;
    no) use_debug=0 
 	 debug_parms="-O2";;
    *) AC_MSG_ERROR(bad value ${enable_debug} for enable debug option) ;;
   esac],
  [use_debug=0])

AC_DEFINE_UNQUOTED(DEBUG, ${use_debug}, [ set to 1 if you want that some debugging output to be send to the auth log ])

AC_ARG_WITH(registry-include, dnl
  --with-registry-include=DIR  find Registry headers in DIR,
            [dnl
case "$with_registry_include" in
''|no) ;;
*) libregistry_include="-I$withval" ;;
esac
])

# Check for a --with-registry-lib argument.
AC_ARG_WITH(registry-lib, dnl
  --with-registry-lib=DIR         find Registry library in DIR,
            [dnl
case "$with_registry-lib" in
''|no) ;;
*) libregistry_libs="-L$withval" ;;
esac
])

REGISTRY_CFLAGS="$libregistry_include"
REGISTRY_LIBS="$libregistry_libs"

CPPFLAGS="$CPPFLAGS $REGISTRY_CFLAGS"
AC_SUBST(REGISTRY_CFLAGS)
AC_SUBST(REGISTRY_LIBS)
AC_SUBST(slibdir)
AC_SUBST(debug_parms)

AC_CONFIG_FILES([Makefile
                 src/Makefile tools/Makefile])
AC_OUTPUT
